set positional-arguments
set fallback := true
set shell := ["bash", "-cue"]

comp_dir := justfile_directory()
root_dir := `git rev-parse --show-toplevel`

build *args:
    cd "{{comp_dir}}" && cargo build "${@:1}"

build-image *args:
    docker build --progress plain "${@:1}" -t "kikist-client-cli:test" -f docker/cli/Dockerfile "{{root_dir}}"

watch:
    cd "{{comp_dir}}" && cargo watch -x 'build'

format:
    cd "{{comp_dir}}" && \
    	docker run -v "{{root_dir}}:/repo" -v "$(pwd):/workspace" -w "/workspace" \
    	instrumentisto/rust:nightly-alpine cargo fmt -- --config-path /repo

lint *args:
    cd "{{comp_dir}}" && \
        {{root_dir}}/tools/run-rust-lint.sh {{args}}

run *args:
    echo "Running client:"
    cargo run --quiet -- {{args}}
